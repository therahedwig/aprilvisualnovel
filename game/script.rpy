# You can place the script of your game in this file.

# Declare images below this line, using the image statement.
# eg. image eileen happy = "eileen_happy.png"
image bg cc = "backgrounds/cfv_april_cardcapital.png"
image bg store = "backgrounds/cfv_april_storeroom.png"
image bg park = "backgrounds/cfv_april_park.png"
image bg under = "backgrounds/cfv_april_underground.png"
image bg ffhq = "backgrounds/cfv_april_ffhq.png"
image bg ffhqi = "backgrounds/cfv_april_ffhqinside.png"
image bg ffhqr = "backgrounds/cfv_april_ffhqren.png"
image bg hotel = "backgrounds/cfv_april_bar.png"
image bg black = "sfx/black.png"

image cg deckgrab  = "sfx/cg_steal.png"
image cg yell  = "sfx/cg_stolen.png"
image cg run  = "sfx/black.png"
image cg kai  = "sfx/cg_kai.png"
image cg aichi  = "sfx/cg_aichi.png"
image cg clouds  = "sfx/bg_clouds.png"

image ikai  = "sfx/fx_kaishock.png"
image imiwa  = "sfx/fx_miwashock.png"
image imisa  = "sfx/fx_misashock.png"
image logo_miwa = Image("sfx/logo_miwa.png")
image logo_kaichi = Image("sfx/logo_kaichi.png")
image ibread = "sfx/black.png"
image i plan1 = "sfx/i_plan1.png"
image i plan2 = "sfx/i_plan2.png"
image i plan3 = "sfx/i_plan3.png"
image i plan4 = "sfx/i_plan4.png"
image ifire = "sfx/i_fire.png"
image ibread = "sfx/i_bread.png"
image imagazine = "sfx/i_magazine.png"
image icloset = "sfx/i_closet.png"


image miwa base = Image("sprites/cfv_april_miwa_basic.png")
image miwa prop = "sprites/cfv_april_miwa_propose.png"
image miwa diss = "sprites/cfv_april_miwa_dissapoint.png"
image miwa think = "sprites/cfv_april_miwa_thinking.png"
image miwa appal = "sprites/cfv_april_miwa_appaled.png"
image miwa angry = "sprites/cfv_april_miwa_angry_defence.png"
image miwa point = "sprites/cfv_april_miwa_angry_offence.png"
image miwa shock = "sprites/cfv_april_miwa_shocked.png"
image miwa embar = "sprites/cfv_april_miwa_embarassed.png"

image miwa basem = "sprites/cfv_april_miwa_basic_m.png"
image miwa propm = "sprites/cfv_april_miwa_propose_m.png"
image miwa dissm = "sprites/cfv_april_miwa_dissapoint_m.png"
image miwa thinkm = "sprites/cfv_april_miwa_thinking_m.png"
image miwa appalm = "sprites/cfv_april_miwa_appaled_m.png"
image miwa angrym = "sprites/cfv_april_miwa_angry_defence_m.png"
image miwa pointm = "sprites/cfv_april_miwa_angry_offence_m.png"
image miwa shockm = "sprites/cfv_april_miwa_shocked_m.png"
image miwa embarm = "sprites/cfv_april_miwa_embarassed_m.png"

image kai base = "sprites/cfv_april_kai_base.png"
image kai anno = "sprites/cfv_april_kai_annoyed.png"
image kai skep = "sprites/cfv_april_kai_skeptical.png"
image kai conf = "sprites/cfv_april_kai_confused.png"
image kai vcon = "sprites/cfv_april_kai_very_confused.png"
image kai happy = "sprites/cfv_april_kai_happy.png"

image misa base = "sprites/cfv_april_misaki_base.png"
image misa basem = "sprites/cfv_april_misaki_base_m.png"
image misa shock = "sprites/cfv_april_misaki_shocked.png"
image misa shockm = "sprites/cfv_april_misaki_shocked_m.png"
image misa smile = "sprites/cfv_april_misaki_smile.png"
image misa smilem = "sprites/cfv_april_misaki_smile_m.png"
image misa sad = "sprites/cfv_april_misaki_sad.png"
image misa sadm = "sprites/cfv_april_misaki_sad_m.png"
image misa anno = "sprites/cfv_april_misaki_annoyed.png"
image misa annom = "sprites/cfv_april_misaki_annoyed_m.png"
image misa angry = "sprites/cfv_april_misaki_angry.png"
image misa angrym = "sprites/cfv_april_misaki_angry_m.png"

image aichi base = "sprites/cfv_april_aichi_base.png"
image aichi basem = "sprites/cfv_april_aichi_base_m.png"
image aichi sbase = "sprites/cfv_april_aichi_h_base.png"
image aichi sbasem = "sprites/cfv_april_aichi_h_base_m.png"
image aichi shock = "sprites/cfv_april_aichi_shock.png"
image aichi shockm = "sprites/cfv_april_aichi_shock_m.png"
image aichi sshock = "sprites/cfv_april_aichi_h_shock.png"
image aichi sshockm = "sprites/cfv_april_aichi_h_shock_m.png"
image aichi pens = "sprites/cfv_april_aichi_pensive.png"
image aichi pensm = "sprites/cfv_april_aichi_pensive_m.png"
image aichi spens = "sprites/cfv_april_aichi_h_pensive.png"
image aichi spensm = "sprites/cfv_april_aichi_h_pensive_m.png"
image aichi smile = "sprites/cfv_april_aichi_smile.png"
image aichi smilem = "sprites/cfv_april_aichi_smile_m.png"
image aichi ssmile = "sprites/cfv_april_aichi_h_smile.png"
image aichi ssmilem = "sprites/cfv_april_aichi_h_smile_m.png"

image kamui base = "sprites/cfv_april_kamui_confused.png"
image kamui smirk = "sprites/cfv_april_kamui_smirk.png"
image kamui yell = "sprites/cfv_april_kamui_yell.png"

image jun base = "sprites/cfv_april_jun_base.png"
image jun think = "sprites/cfv_april_jun_think.png"

image asaka = "sprites/cfv_april_asaka.png"

image boy base = "sprites/cfv_april_boy_base.png"
image boy skep = "sprites/cfv_april_boy_skep.png"
image boy arro = "sprites/cfv_april_boy_arro.png"

image ren base = "sprites/cfv_april_ren_base.png"
image ren basem = "sprites/cfv_april_ren_base_m.png"
image ren smile = "sprites/cfv_april_ren_smile.png"
image ren smilem = "sprites/cfv_april_ren_smile_m.png"
image ren regard = "sprites/cfv_april_ren_regard.png"
image ren regardm = "sprites/cfv_april_ren_regard_m.png"

# Declare characters used by this game.
define e = Character('Eileen', color="#c8ffc8")
define miw = Character('Miwa', color="#bcbcbd")
define mis = Character('Misaki', color="#d9b0e7")
define kai = Character('Kai', color="#09cba4")
define ren = Character('Ren', color="#cb012c")
define jun = Character('Jun', color="#426572")
define ai = Character('Aichi', color="#13568a")
define asa = Character('Asaka', color="#6198db")
define kam = Character('Kamui', color="#ef9a20")


# The game starts here.
label start:
    $ sleft =  Position(xalign=0.15, yalign=1.0, xanchor=0.3, yanchor=1.0)
    $ middle = Position(xalign=0.5, yalign=0.4, xanchor=0.5, yanchor=0.5)
    $ sleftm = Position(xalign=0.15, yalign=1.0, xanchor=0.6, yanchor=1.0)
    $ sright =  Position(xalign=0.85, yalign=1.0, xanchor=0.6, yanchor=1.0)
    $ srightm = Position(xalign=0.85, yalign=1.0, xanchor=0.3, yanchor=1.0)
    $ scenter = Position(xalign=0.5, yalign=1.0, xanchor=0.3, yanchor=1.0)
    $ scenterm = Position(xalign=0.5, yalign=1.0, xanchor=0.6, yanchor=1.0)
    $ flash = Fade(.25, 0, .75, color="#fff")
    $ in_bread = False
    $ in_stocks = False
    $ in_fire = False
    
    scene cg deckgrab
    $ renpy.pause(1)
    scene cg yell with flash
    show imisa at offscreenleft
    show ikai at offscreenright
    show imiwa at offscreenright
    "Boy" "my deck is gone!"
    #Closeups of Misaki, Kai and Miwa.#
    show imisa at center with flash
    show ikai at center with flash
    show imiwa at center with flash
    $ renpy.pause(2)
    scene bg black with fade
    "And it was such a quiet day too..."
    scene bg cc with fade
    show misa base at sleft
    mis "What is going on?"
    show boy base at sright
    "Boy" "My deck is missing!"
    show misa basem at sleftm
    mis "Ah, everyone, did anyone see this boy's deck? The clan is..."
    "Boy" " Oracle Think-Tank."
    #Misaki looks shocked
    show misa shock at sleft
    mis "...Oracle Think-Tank."
    #Miwa ace detective flashes on screen.
    scene bg black
    show logo_miwa at middle
    with flash
    $ renpy.pause(3)
    
    #Shop is visible with kai, misaki and miwa figures.
    scene bg cc
    show i plan1 at offscreenleft
    show miwa base at sleft
    show misa basem at sright
    with fade
    miw "So this kid was distracted and poof, someone nicked the deck while he wasn't looking?"
    mis "I saw that the boy was playing, and next thing his deck is stolen."
    miw " And neither you or the assistant-manager saw anything? Anyone leave the shop?"
    mis "No, not at all."
    miw "That's new."
    mis "Ayup."
    show miwa think
    miw "Let's asume that there's nothing wrong with with your eyesight."
    #map of the shop
    show i plan1 at middle
    miw "The boy was here when the deck got stolen, right?"
    #boy's location is marked
    show i plan2 at middle
    mis "Correct."
    miw "You were at you usual spot with the sub-manager, right here near the door."
    show i plan3 at middle
    #misaki's location is marked
    show i plan4 at middle
    miw "You can't have seen the cards being stolen because it was behind the wall."
    miw "But you didn't see anybody leave, which means..."
    #The door is marked
    mis "They went into the storeroom?"
    show miwa prop
    miw "Exactly"
    show misa shockm
    mis "C'mon!"
    
    #Storeroom background, Misaki figure appears.
    show ibread at offscreenleft
    show ifire at offscreenleft
    scene bg store with fade
    show miwa shock at sleft
    show misa shockm at sright
    miw "There's noone here."
    mis "But that's impossible..."
    show miwa think
    show misa basem
    miw "Hm... Let's see what we can find."
label investigate:    
    #Player has the ability to check things out now: *door, breadcrumbs, the stocks*
    show miwa think
    show misa basem
    menu:
        miw "What shall we take a look at?"
        "Are those breadcrumbs?":
            jump qbread
        "Check the stocks":
            jump qstocks
        "Check the door":
            jump qfireescape

label qbread:
    #miwa determined
    show ibread at middle
    show miwa point at sleft
    miw "Aha! Breadcrumbs! Get me some iodine!"
    show misa shockm
    mis "What? You mean to check for starch?"
    show miwa base at sleft
    miw "Indeed, my dear Misaki!"
    show misa annom
    #misaki annoyed
    mis "Can't you do something normal? Like, fingerprinting? Or going to the police?"
    show miwa prop
    miw "Well, that's elementary, isn't it? The police won't take us seriously at all! And what are we supossed to fingerprint in the first place?"
    show misa basem
    mis "Right, but... Miwa, all bread contains starch, so iodine isn't going to solve this case."
    show misa annom
    mis "Besides, those are mine, I had a snack after changing the posters yesterday."
    show miwa diss
    miw "Fine."
    hide ibread
    $ in_bread = True
    if in_stocks and in_fire and in_bread:
        jump investigateconclude
    else:
        jump investigate
label qstocks:
    #the stocks
    show miwa base at sleft
    show misa base at srightm
    miw "(Misaki is looking at the stocks...)"
    mis "If they stole a deck, who knows what else they've stolen"
    #Misaki looks relieved#
    show misa basem at sright
    mis "I can't see anything missing."
    #Miwa smiles
    show miwa prop at sleft
    miw "That's good."
    $ in_stocks = True
    if in_stocks and in_fire and in_bread:
        jump investigateconclude
    else:
        jump investigate
label qfireescape:
    #the fireescape
    miw "Can this door open?"
    show misa shockm
    mis "Miwa, wait!"
    show ifire at middle
    "Fire-alarm starts to ring!"
    "Oooooeeeeeeuuuuu-Oooooeeeeeeuuuuu"
    #picture of door opening, fire alarm starts to ring
    show misa angrym
    mis "Now you've done it!"
    scene bg black with fade
    #screen goes black. firealarm cuts off.
    scene bg store with fade
    show misa annom at sright
    show miwa embar at sleft
    mis "Next time, ask if you can open the Fire exit."
    show miwa embar
    miw "Sorry."
    show miwa think
    miw "(but we now know the thief didn't leave through here.)"
    $ in_fire = True
    if in_stocks and in_fire and in_bread:
        jump investigateconclude
    else:
        jump investigate
label investigateconclude:
    #conclusion
    #mis "Let's give up, there's nothing here..."
    scene bg cc
    show kai base at sleft
    with fade
    show miwa dissm at offscreenright
    show misa basem at offscreenright
    kai "And?"
    show miwa dissm at sright with move
    miw "Dead end..."
    #Misaki sad
    show misa sadm at sright 
    show miwa base at scenter
    with move
    miw "Hey..."
    show miwa prop
    miw "Don't worry! We'll find the thief."
    show kai skep
    kai "Miwa."
    kai "Maybe you're better off looking for the thief outside."
    #miwa mirrored
    show miwa basem at scenterm
    miw "Good plan!"
    show miwa thinkm at sleftm
    show kai base at scenter
    with move
    miw "Where should I start...hm..."
    #hide miwa with disolve
    show miwa thinkm at offscreenleft with move
    kai "Tokura!"
    show misa basem
    show kai base at sleft with move
    #Kai asking who should go with Miwa
    kai "Someone should go with him, the last time I left him to his own devices, I found him tied up by the backstreet boys."
    show misa smilem
    mis "Tell me why?"
    kai "No."
    show kai anno
    kai "Ain't nothing but a headache."
    show kai base
    kai "Shall I go?"
    show misa basem
    mis "Hm..."
    menu:
        "I'll go.":
            jump misakiroute
        "Let Kai go":
            jump kairoute
        
label misakiroute:
    scene bg park
    show miwa base at scenter
    show misa base at offscreenleft
    with fade
    show misa base at sleft with move
    mis "Miwa, I'm coming with you!"
    show miwa shockm at scenterm
    miw "Ah?"
    show miwa basem
    miw "Oh, I didn't realise you'd want to find this guy too"
    #Misaki looks sad
    show misa sad
    show miwa thinkm
    miw "(Damnit...)"
    miw "(They don't have many expressions, but a sad one is the last I would like to see on their faces...)"
    show miwa propm
    miw "Let's do our best."
    mis "Yeah..."
    show misa anno
    mis "Do you have any idea where to look?"
    show miwa shockm
    miw "Ah..."
    show miwa thinkm
    miw "Oracle Think-Tank is pretty expensive these days, isn't it?"
    show misa base
    mis "Yeah, it's a pretty popular clan. That kid had some SPs in his deck as well."
    miw "Alright, there might be a place where I think they might know something..."
    scene bg under
    show miwa base at scenter
    show misa base at sleft
    show jun base at offscreenright
    with fade
    miw "This is the spot."
    show miwa basem at scenterm
    mis "This place... they hold underground fights here, don't they?"
    mis "How did you find it in the first place?"
    jun "Miwa!"
    show miwa shock at scenter
    show jun base at sright with move
    jun "I haven't seen you around lately!"
    jun "Did you mind being tied up that much?"
    show misa anno
    mis "Oh, I suposse that answers my question."
    show miwa appalm at scenterm
    miw "What? "
    extend "No!"
    miw "It's not what it sounds like!"
    extend " Don't go thinking crazy things now!"
    show jun think
    show miwa angrym
    show misa smile
    #Misaki looks amused
    mis "Too late for that."
    show jun base
    jun "You really have the best friends."
    jun "But if you've come to deliver another one of his girlfriends, he isn't here right now."
    show misa base
    show miwa embar
    miw "I'm not, don't worry."
    show miwa think
    miw "Where to start explaining..."
    #screen fades to black for a bit
    scene bg black with fade
    scene bg under
    show misa base at sleft
    show miwa prop at scenter
    show jun base at sright
    with fade
    miw "So, we figured you might know something..."
    show miwa base
    show jun think
    jun "Hm..."
    jun "I haven't heard of any decks being pawned yet,"
    extend " and frankly noone here plays Think-tank."
    jun "But, "
    extend "the Foo-Fighters recruited a real Think-tank heavy weight recently."
    show jun base
    jun "You might want to ask them if they know anything."
    show miwa think
    miw "I see..."
    show miwa basem at scenterm
    miw "Thanks!"
    jun "Just visit more often, will you?"
    miw "Right."
    show miwa propm
    miw "Let's go!"
    show miwa basem at offscreenleft with move
    show misa basem at offscreenleft with move
    hide miwa
    hide misa
    show jun think
    jun "He didn even answer me, did he?"
    
    #Visiting Ren
    scene bg ffhqi
    show miwa basem at scenterm
    show misa base at sleft
    show boy base at offscreenright
    with fade
    show miwa propm
    miw "Now, let's see if we can find that guy..."
    show misa shock
    mis "Ah!"
    show miwa base at scenter
    miw "What is it?"
    show miwa base at offscreenleft
    show misa base at offscreenleft
    show boy at scenterm
    with move
    mis "Look"
    #boy is shown
    miw "No way."
    show miwa base at scenter
    show misa anno at sleft
    show boy at sright
    with move
    "Boy" "Ah, you guys?!"
    show miwa angry
    miw "Your deck was stolen, huh?"
    miw "You know what I think?"
    show miwa point
    miw "your deck wasn't stolen in the first place!"
    show boy skep
    "Boy" "-tchk"
    show miwa angry
    miw "Instead, you took your deck with you!"
    show miwa point
    miw "Misaki, nor the submanager would have suspected you of stealing your own deck!"
    show misa anno
    mis "How dare you! I can't believe we favour the same clan!"
    show boy arro
    "Boy" "Very nice, Detective."
    show boy skep
    "Boy" "But tell me, what would be my motive?"
    show miwa prop
    miw "Well that's-"
    show miwa shock
    miw "Uhm..."
    mis "Suzugamori Ren..."
    show misa shock
    mis "But that means!"
    show miwa shockm
    show misa shockm at offscreenleft with move
    #misaki runs off
    miw "Hey, where you going to?"
    show miwa shockm at offscreenleft with move
    #Miwa runs off after her
    #Misaki comes to the conclusion this was set up by Kai somehow and goes out to murder him.
    show asaka at offscreenright
    show ren basem at scenterm with move
    ren "Ah? Where is Misakki?"
    show boy arro
    "Boy" "They left for some reason."
    show ren regardm
    ren "Really? That's no fun..."
    hide boy
    show asaka at sright with move
    asa "Ren-sama! Why did you try to get that Tokura girl here anyway?"
    show ren regard at sleft with move
    ren "Asaka!"
    show ren base
    ren "Ah, I needed some help with moving furniture!"
    show ren smile
    ren "I can't ask that of you, my dear..."
    asa "What? I'm an expert a moving things!"
    show ren regard
    ren "Really?"
    asa "Why yes, I'm like,"
    extend "a genuine moverella!"
    show ren smile
    ren "Oh, well in that case..."
    #scene fade
    
    scene bg cc
    show kamui base at offscreenright
    show miwa propm at offscreenright
    show kai base at sleft
    show misa annom at offscreenright
    with fade
    show misa annom at scenterm with move
    mis "Kai Toshiki!"
    show misa angrym
    extend " Did you think you could get away with this?"
    show kai anno
    kai "Why the hell are you yelling at me, Tokura?"
    show misa annom
    mis "You knew about this, you!"
    show misa annom at offscreenleft
    show kai anno at offscreenleft
    show kamui base at sright
    show miwa shockm at scenterm
    with move
    
    kam "What the hell?"
    miw "I'm not really sure what is going on either..."
    kam "..."
    show kamui yell
    kam "Want me to hold him while you punch him?"
    show miwa prop
    miw "Aren't you a little short for that?"
    show kamui smirk
    kam "That's what you think."
    show misa annom at scenterm
    show kai vcon at sleft
    show kamui base at offscreenright
    show miwa propm at offscreenright
    with move
    kai "Maybe it's time you learn the world doesn't revolve around you Tokura!"
    show misa angrym
    mis "Really, that's rich coming from you!"
    show misa angrym at offscreenleft
    show kai anno at offscreenleft
    show kamui base at sright
    show miwa thinkm at scenterm
    with move
    miw "I don't think she needs any help though..."
    kam "I guess you're right."
    show kamui smirk
    kam "Hey, you up for a game?"
    show miwa base
    miw "Sure."
    scene bg black with fade
    "Thankfully, Kai and Misaki got past their misunderstanding eventually."
    "Man, this day was strange..."
    centered "Congratulations, you've finished Misaki's route!"
    $ persistent.misaki_fin = True
    #check if both endings played, goto secret ending
    if persistent.misaki_fin and persistent.kai_fin:
        jump kaichihoneymoon
    else:
        centered "What do you think Kai's route is like?"
        return
label kairoute:
    
    #Kai's route
    #Meeting with Aichi
    scene bg park
    show kai base at offscreenleft
    show miwa base at scenter
    show aichi basem at offscreenright
    with fade
    show kai base at sleft with move
    kai "Miwa."
    show miwa shockm at scenterm
    kai "I'm coming with you."
    show miwa basem
    miw "Ah, you're coming too?"
    show miwa propm
    miw "Great!"
    show kai skep
    kai "What are your plans?"
    show miwa embarm
    miw "Eh... Let me think..."
label nowhwat:
    show miwa think
    menu:
        miw "What shall I do?"
        "Ask Kai what he thinks.":
            jump askkai
        "What is Kai looking at?":
            jump meetaichi

label askkai:
    show miwa propm
    miw "Kai, you really want to find this guy huh?"
    show kai anno
    kai "Stealing is unforgivable."
    kai "That kind of behaviour can't go unpunished!"
    show miwa embarm
    miw "(Why do I have the feeling you are going to punch the thief instead of face them through vanguard?)"
    show miwa prom
    miw "Hey, with you and me teaming up, there's no way this guy can get away!"
    kai "..."
    show miwa embarm
    miw "(I really wouldn't want to be the thief...)"
    jump nowhwat
    
label meetaichi:
    show miwa angry 
    miw "What are you looking at anyway?"
    show miwa shock
    miw "Eh?"
    show miwa base
    miw "(Congratulations, Kai, you found Aichi"
    show miwa diss
    extend " ...again.)"
    show miwa basem
    miw "(If that kid ever gets missing, I'll recommend you with the tracking dog team.)"
    show miwa base
    miw "Aichi!"
    show aichi smilem at sright with move
    ai "Miwa!"
    extend " Kai too!"
    show aichi basem
    ai "You guys aren't going to the Card Capital today?"
    miw "Ah, well"
    scene bg black with fade
    scene bg park
    show kai base at sleft
    show miwa prop at scenter
    show aichi shockm at sright
    with fade
    
    ai "That's horrible."
    show aichi pensm
    ai "So you are looking for the thief now?"
    show miwa think
    miw "Yeah, but we've got no leads now."
    kai "Aichi."
    show miwa shock
    extend " What is that you have there?"
    show aichi shockm
    ai "Eh? This is Vanguard Monthly..."
    show aichi basem
    ai "Komoi gave it to me."
    show miwa think
    miw "Wait, let me see that."
    miw "..."
    show miwa shock
    "Eh?!"
    show imagazine at middle
    show kai vcon
    show aichi shockm
    miw "This is the victim!"
    ai "What?"
    miw "Foo-fighter Think-tank... In search of new talent... Oracle think-tank genius... "
    ai "But what is a guy like this doing at the card-capital?"
    hide imagazine
    show miwa think
    miw "..."
    show miwa prop
    miw "Aha! I see now,"
    show miwa point with flash
    extend " it must be his evil twin brother!"
    show aichi shockm
    ai "No way!"
    show kai skep
    kai "..."
    show miwa think
    miw "I feel like a fierce sibling rivalry is at the bottom of this!"
    miw "Two brothers, both playing Oracle Think-tank."
    extend " And then one got jealous of the other."
    show miwa point
    miw "We MUST put and end to this!"
    miw "To the Foo-fighter HQ!"
    show miwa base at offscreenright
    show aichi base at scenter
    with move
    ai "Uhm, Miwa, the Foo-fighter HQ is the other way"
    show miwa embarm at sright with move
    miw "Ah, got a little carried away"
    show miwa pointm
    miw "Kai, Aichi!"
    extend " Lead the way!"
    #Visiting Ren
    scene bg ffhqi
    show kai base at scenter
    with fade
    show ren basem at sright
    ren "Kai, what a surprise to see you!"
    show aichi base at sleft
    ai "Ren!"
    show ren smilem
    ren "And Aichi too, you three are the only ones?"
    hide aichi
    show miwa angry at sleft
    miw "Yes"
    show miwa point
    show kai smile
    miw "We're here to end a sibling rivalry!"
    show ren basem
    ren "Really, that sounds exciting!"
    show kai skep
    show miwa shock
    kai "Ren, why did you ask that kid to pretend his deck was stolen?"
    show ren regardm
    ren "Ah, well..."
    show ren smilem
    ren "I had hoped on visit from a lovely shop assistant."
    show kai base
    kai "I did bring one."
    show miwa base
    miw "Hi!"
    show ren regardm
    ren "Hm? Detective, Shop assistant, you're quite versatile, aren't you?"
    show ren smilem
    ren "But I had hoped on someone with wider hips."
    show kai anno
    kai "She didn't feel like coming."
    show ren regardm
    ren "Really now?"
    show ren smilem
    ren "Oh well, at the least we have Aichi and... Of Course..."
    show kai base at sleft
    show miwa base at scenter
    with move
    show ren basem
    kai "This is Miwa... Taishi. He plays Kagero... Is an Aries... And..."
    show kai skep
    kai "Likes long walks along the beach?"
    show miwa dissm at scenterm
    miw "Really?"
    show ren regardm
    ren "Shop assistant huh?"
    show ren basem
    ren "You'll do, c' mon."
    show miwa shock
    miw "huh?"
    show kai smile
    kai "If you use lube it'll go easier."
    show miwa appal
    miw "WHAT?"
    hide miwa
    hide ren
    show kai anno at scenter with move
    show aichi base at sleft
    kai "Aichi, let's leave."
    show aichi shock
    ai "Eh? okay."
    
    scene bg ffhq
    show kai anno at sleft
    show aichi pensm at sright
    with fade
    ai "Kai, what exactly did Ren need Miwa for?"
    show kai vcon
    kai "Eh..."
    show kai anno
    kai "They're putting together furniture."
    show aichi shockm
    ai "Eh?"
    
    scene bg ffhqr
    show icloset at offscreenright
    #shake screen
    show ren basem at sright
    ren "There!"
    show miwa prop at sleft
    miw "Great."
    show miwa shock
    miw "Huh, lube did make it easier."
    show icloset at middle
    show miwa think
    miw "But how come Kai knows so much about putting together furniture?"
    show ren regardm
    ren "Ah..."
    scene cg kai with fade
    ren "Kai is from an ancient clan of furniture movers"
    ren "They had a secret technique: Demonic furniture moving, that was passed down from father to son."
    ren "Kai is the last one who knows this ancient forbidden technique."
    scene bg ffhqr
    show ren base at sright
    show miwa think at sleft
    with fade
    miw "Huh, and you think you know a guy..."
    show ren regardm
    ren "Hm."
    show ren basem
    ren "Kagero right?"
    show ren smilem
    ren "Let's have a go."
    show miwa base
    miw "Sure!"
    #ren ""
    #miw ""
    scene bg black with fade
    "Ren is a little strange, but we had fun."
    "Kinda had muscleaches next day though..."
    "Man, this day was strange..."
    centered "Congratulations, you've finished Kai's route!"

    $ persistent.kai_fin = True
    #check if both endings played, then goto secret ending.
    if persistent.misaki_fin and persistent.kai_fin:
        jump kaichihoneymoon
    else:
        centered "What do you think Misaki's routa was like?"
        return
    
label kaichihoneymoon:
    scene bg black
    centered "You've finished both endings, I guess I should give you what you came for..."
    centered "Alright, I'm ready."
    scene cg clouds with fade
    show logo_kaichi at middle with flash
    $ renpy.pause(2)
    scene cg aichi with fade
    ai "Ah"
    ai "Kai-kun"
    scene bg hotel
    show kai base at sleft
    show aichi sbasem at sright
    with fade
    kai "Aichi,"
    show kai skep
    extend " what the hell are you wearing?"
    show aichi sshockm
    ai "Kai-kun! That should be obvious!"
    show kai anno
    show aichi sbasem
    #kai-kun looks disturbed now
    ai "Despite me identifying like a boy, I look like a girl, so therefore I like dressing like one!"
    show aichi sshockm
    ai "Kai-kun, don't you know anything about gender-politics?"
    show kai conf
    kai "Eh..."
    show aichi spens
    ai "I knew I should've gone to Ren-san with this."
    show aichi sshock
    ai "At the least he's mature enough to... deal with my needs."
    show kai vcon
    show aichi sshockm
    kai "W-what?"
    ai "Sayonara, Kai-kun!"
    show aichi sshock at offscreenright with move
    #aichi goes off-screen
    kai "What?"
    #pause
    $ renpy.pause(1)
    ai "Kai-kun?"
    show aichi spensm at sright with move
    #aichi moves on screen
    show aichi ssmilem
    ai "April-fools, Kai-kun!"
    show kai conf
    kai "What...?"
    ai "April-fools!"
    show kai skep
    kai "This was fake."
    show kai base
    extend " Good."
    show aichi spensm
    ai "I didn't scare you too much, did I?"
    show kai skep
    kai "No. You didn't."
    extend " But."
    show kai anno
    extend " What was this about Ren being more mat-"
    ai "Ah, Kai-kun?"
    show aichi sbasem
    ai "Would you mind if I... slip into something more comfortable?"
    show kai conf
    kai "-ure, what?"
    show aichi ssmilem
    ai "I would like to go change into my regular clothes now,"
    show aichi spensm
    extend " this is really embarassing to wear in public..."
    show kai vcon
    kai "Eh."
    show kai base
    show aichi ssmilem
    kai "Yes, you do that!"
    show aichi sbase at offscreenright with move
    #Aichi leaves
    show kai vcon
    kai "(What the flying fuck?)"
    
    #hide scene bg hotel with fade
    scene bg black
    centered "Happy April Fools, everyone!"
    centered "Credits: Story/Programming/Art: Wolthera (wolthera.tumblr.com)"
    centered "Testing:"
    centered "Made with the following software:
              Ren'py,
              Inkscape,
              Krita"
    
    return
